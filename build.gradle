buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
plugins {
    id 'com.matthewprenger.cursegradle' version '1.3.0'
    id 'se.bjurr.gitchangelog.git-changelog-gradle-plugin' version '1.61'
    id 'org.ajoberstar.reckon' version '0.11.0'
    id 'org.ajoberstar.grgit' version '3.1.1'
    id 'com.github.ben-manes.versions' version '0.21.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

group= 'cpw.mods'
archivesBaseName = 'inventorysorter'

reckon {
    scopeFromProp()
    stageFromProp('ms', 'final')
}

minecraft {
    mappings channel: 'snapshot', version: '20190621-1.14.2'
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'
            property 'forge.logging.noansi', 'false'
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'
            property 'forge.logging.noansi', 'false'
        }
    }
}


dependencies {
    minecraft "net.minecraftforge:forge:${MC_VERSION}-${FORGE_VERSION}"
}

task makeChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = file('CHANGELOG.md')
    untaggedName = "Current release ${version}"
    fromRef = project.findProperty('changelogStart') ?: '13.0'
    templateContent = """
# Inventory sorter changelog history

## Version ${version} for Minecraft ${MC_VERSION}

{{#tags}}
* {{name}}
  {{#commits}}
  * {{{messageTitle}}}
  {{/commits}}
{{/tags}}
"""
}

curseforge {
    apiKey = project.hasProperty('curseforge_apikey') ? project.curseforge_apikey : '0'
    project {
        id = '240633'
        changelog = file('CHANGELOG.md')
        changelogType = 'markdown'
        releaseType = project.findProperty('releaseType') ?: 'alpha'
    }
    options {
        javaVersionAutoDetect = true // defaults to true
        javaIntegration = true // defaults to true
        forgeGradleIntegration = true // defaults to true
    }
}

afterEvaluate {
    tasks.curseforge240633.dependsOn.remove(reobfJar)
    tasks.curseforge240633.dependsOn.remove(jar)
    tasks.curseforge240633.dependsOn.add(makeChangelog)
}

jar {
    manifest {
        attributes([
                "Specification-Title": 'inventorysorter',
                "Specification-Vendor": 'cpwmods',
                "Specification-Version": '1', // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :'cpwmods',
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    appendix MC_VERSION
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            credentials {
                username project.properties.cpwMavenUser?:'spam'
                password project.properties.cpwMavenPassword?:'bums'
            }
            url 'http://files.minecraftforge.net/maven/manage/upload'
        }
    }
}
